# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Colin Shea <colins@evaryont.ath.cx>

pkgname=cave9
pkgver=0.4
_dataver=4
pkgrel=1
pkgdesc="A 3D gravity cave-exploration game based on the classic SFCave"
arch=('i686' 'x86_64')
url="http://code.google.com/p/cave9/"
license=('LGPL3' 'custom')
depends=('mesa' 'sdl_image' 'sdl_ttf')
source=("http://cave9.googlecode.com/files/${pkgname}_src-${pkgver}.tgz"
        "http://cave9.googlecode.com/files/${pkgname}_data-${_dataver}.tgz"
        "${pkgname}.desktop")
md5sums=('9b9987c6e98f99ced46b0abefe527ab1'
         '90e12967e9e103b1b5dff0ac15772371'
         '39cf91320b0de2d55d28254a7419c6a1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CFLAGS='' LDFLAGS='' make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install binary
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install data files
  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  cp ../data/*.{jpg,png,ttf,wav} "${pkgdir}/usr/share/${pkgname}"

  # Install pixmap and .desktop file
  install -Dm644 "../data/icon.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install documentation and licenses
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 AUTHORS.txt README.txt "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 ../data/README.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE_DATA.txt"
}

# vim:set ts=2 sw=2 et:
